/*
 * Gradle build script.
 */

plugins {
	id 'java'
	id 'maven-publish'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'org.bsworks.catalina.authenticator.oidc'
version = '2.3.2'

task jar(type: Jar, overwrite: true) {}
jar.enabled = false
javadoc.enabled = false

sourceSets {
	mainTomcat85 {
		java {
			srcDirs = ['src/common/java', 'src/tomcat85/java']
		}
	}
	mainTomcat90 {
		java {
			srcDirs = ['src/common/java', 'src/tomcat90/java']
		}
	}
}

task compileJava(type: JavaCompile, overwrite: true) {
	dependsOn compileMainTomcat85Java, compileMainTomcat90Java
}

ext.sharedManifest = manifest {
	attributes(
		'Implementation-Title': project.name,
		'Implementation-Version': project.version,
		'Implementation-Vendor': 'Boyle Software, Inc.'
	)
}

task mainTomcat85Jar(type: Jar) {
	classifier 'tomcat85-st'
	from sourceSets.mainTomcat85.output
	into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename ".*", "pom.xml"
    }
	manifest = project.manifest {
		from sharedManifest
	}
}
task mainTomcat90Jar(type: Jar) {
	classifier 'tomcat90-st'
	from sourceSets.mainTomcat90.output
	into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename ".*", "pom.xml"
    }
	manifest = project.manifest {
		from sharedManifest
	}
}

repositories {
	mavenCentral()
}

dependencies {
	mainTomcat85Implementation(
		'org.apache.tomcat:tomcat-catalina:[8.5.50,8.6.0)',
		'org.apache.tomcat:tomcat-juli:[8.5.50,8.6.0)',
		'org.apache.tomcat:tomcat-util:[8.5.50,8.6.0)'
	)
	mainTomcat90Implementation(
		'org.apache.tomcat:tomcat-catalina:[9.0.30,9.1.0)',
		'org.apache.tomcat:tomcat-juli:[9.0.30,9.1.0)',
		'org.apache.tomcat:tomcat-util:[9.0.30,9.1.0)'
	)
}

artifacts {
	archives(
		mainTomcat85Jar,
		mainTomcat90Jar
	)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = 'OpenID Connect Authenticator'
				description = 'Apache Tomcat authenticator implementation that extends the standard form-based authenticator and adds OpenID Connect functionality.'
				url = 'https://github.com/boylesoftware/tomcat-oidcauth'
				inceptionYear = '2015'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				organization {
					name = 'Boyle Software, Inc.'
					url = 'https://www.boylesoftware.com/'
				}
				developers {
					developer {
						id = 'levahim'
						name = 'Lev Himmelfarb'
						email = 'lev@boylesoftware.com'
						timezone = 'America/New_York'
					}
				}
				scm {
					connection = 'scm:git:https://github.com/boylesoftware/tomcat-oidcauth.git'
					developerConnection = 'scm:git:git@github.com:boylesoftware/tomcat-oidcauth.git'
				}
				issueManagement {
					system = 'GitHub'
					url = 'https://github.com/boylesoftware/tomcat-oidcauth/issues'
				}
			}
			artifact mainTomcat85Jar
			artifact mainTomcat90Jar
		}
	}
	repositories {
		if(project.hasProperty('mavenUrl')) {
			maven {
				name = 'remote'
				url = project.property('mavenUrl')

				if(project.hasProperty('mavenUsername')) {
					credentials {
						username project.property('mavenUsername')
						password project.property('mavenPassword')
					}
				}
			}
		} else {
			maven {
				name = 'local'
				url = "file://${buildDir}/repo"
			}
		}
	}
}
